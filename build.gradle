plugins {
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'java'

    // test coverage
    id 'jacoco'

    // ide configuration
    id 'eclipse'
    id 'idea'

    // integrationTests
    id 'jvm-test-suite'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2022.0.3")

    jsonPatchVersion = '1.13'

    jUnit5Version = '5.9.3'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-loadbalancer'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    implementation group: 'io.github.openfeign', name: 'feign-httpclient'
    implementation group: 'io.github.openfeign', name: 'feign-okhttp'
    implementation group: 'io.github.openfeign', name: 'feign-hc5'
    implementation(group: "com.github.java-json-tools", name: "json-patch", version: "${jsonPatchVersion}");
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-circuitbreaker-resilience4j'

    implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.2.1'

    testImplementation(platform("org.junit:junit-bom:${jUnit5Version}"))
    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
      because("Only needed to run tests in a version of IntelliJ IDEA that bundles older versions")
    }
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

apply from: 'gradle/ide.gradle'
apply from: 'gradle/testing.gradle'
apply from: 'gradle/jacoco.gradle'

check.dependsOn integrationTest
check.finalizedBy jacocoTestReport
integrationTest.mustRunAfter test

test {
    useJUnitPlatform()
}
